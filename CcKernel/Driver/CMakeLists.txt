########
# Add Source Files
########
file (GLOB IDriver_SOURCES
      "*.cpp"
      "*.h"
)

set( IDriver_HEADERS )
set( IDriver_SOURCES )
set( IDriver_NAMES_0 )
set( IDriver_NAMES_1 )
set( IDriver_NAMES_2 )
set( IDriver_NAMES_3 )
set( IDriver_INCLUDES )
set( IDriver_DEFINITIONS )
set( IDriver_DEPENDENCIES )
set( IDriver_LIBRARIES )

#######
# Setup Driver Macros
#######

macro (add_to_driver_sources)
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_SOURCES "${_src}")
    endforeach()
    set (IDriver_SOURCES ${IDriver_SOURCES} PARENT_SCOPE)
endmacro()

macro (add_to_driver_headers)
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_HEADERS "${_src}")
    endforeach()
    set (IDriver_HEADERS ${IDriver_HEADERS} PARENT_SCOPE)
endmacro()

macro (add_to_driver_names BootOrder)
    if(${BootOrder} EQUAL 0)
      set(TargetList "IDriver_NAMES_0")
    elseif(${BootOrder} EQUAL 1)
      set(TargetList "IDriver_NAMES_1")
    elseif(${BootOrder} EQUAL 2)
      set(TargetList "IDriver_NAMES_2")
    elseif(${BootOrder} EQUAL 3)
      set(TargetList "IDriver_NAMES_3")
    else()
      message(FATAL_ERROR "Wrong boot order: ${BootOrder}")
    endif()

    foreach (_src ${ARGN})
      CcListAppendOnce(${TargetList} "${_src}")
    endforeach()
    set (${TargetList} ${${TargetList}} PARENT_SCOPE)
endmacro()

macro (add_to_driver_includes)
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_INCLUDES "${_src}")
    endforeach()
    set (IDriver_INCLUDES ${IDriver_INCLUDES} PARENT_SCOPE)
endmacro()

macro (add_to_driver_definitions)
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_DEFINITIONS "${_src}")
    endforeach()
    set (IDriver_DEFINITIONS ${IDriver_DEFINITIONS} PARENT_SCOPE)
endmacro()

macro (add_to_driver_dependencies)
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_DEPENDENCIES "${_src}")
    endforeach()
    set (IDriver_DEPENDENCIES ${IDriver_DEPENDENCIES} PARENT_SCOPE)
endmacro()

macro (add_to_driver_libraries)
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_LIBRARIES "${_src}")
    endforeach()
    set (IDriver_LIBRARIES ${IDriver_LIBRARIES} PARENT_SCOPE)
endmacro()

macro (add_to_driver_onload Device)
    CcListAppendOnce(IDriver_ONLOAD ${Device})
    foreach (_src ${ARGN})
        CcListAppendOnce(IDriver_ONLOAD_${Device} "${_src}")
    endforeach()
    set (IDriver_ONLOAD_${Device} ${IDriver_ONLOAD_${Device}} PARENT_SCOPE)
    set (IDriver_ONLOAD           ${IDriver_ONLOAD}           PARENT_SCOPE)
endmacro()

macro (push_up_driver_vars)
  add_to_driver_sources     ( ${IDriver_SOURCES} )
  add_to_driver_headers     ( ${IDriver_HEADERS} )
  add_to_driver_names       ( 0 ${IDriver_NAMES_0} )
  add_to_driver_names       ( 1 ${IDriver_NAMES_1} )
  add_to_driver_names       ( 2 ${IDriver_NAMES_2} )
  add_to_driver_names       ( 3 ${IDriver_NAMES_3} )
  add_to_driver_includes    ( ${IDriver_INCLUDES} )
  add_to_driver_definitions ( ${IDriver_DEFINITIONS} )
  add_to_driver_dependencies( ${IDriver_DEPENDENCIES} )
  add_to_driver_libraries   ( ${IDriver_LIBRARIES} )
  set (IDriver_ONLOAD           ${IDriver_ONLOAD} PARENT_SCOPE)
  foreach (Device ${IDriver_ONLOAD})
    set (IDriver_ONLOAD_${Device} ${IDriver_ONLOAD_${Device}} PARENT_SCOPE)
  endforeach()
  push_up_kernel_vars()
endmacro()

################################################################################
# Choose Driver depending on OS
################################################################################
add_subdirectory(Camera)
add_subdirectory(CPU)
add_subdirectory(Board)
add_subdirectory(Network)
add_subdirectory(SPI)
add_subdirectory(Wlan)
add_subdirectory(Display)

################################################################################
# Write Data to Driverload.cpp
################################################################################
set(CcDriverLoad_TEMP    ${CMAKE_CURRENT_BINARY_DIR}/CcDriverLoad.cpp.tmp)
set(CcDriverLoad_TARGET  ${CMAKE_CURRENT_BINARY_DIR}/CcDriverLoad.cpp)
file(WRITE ${CcDriverLoad_TEMP} "/* Generated File */\n")
file(APPEND ${CcDriverLoad_TEMP} "#include \"CcKernel.h\"\n")
file(APPEND ${CcDriverLoad_TEMP} "#include \"Driver/CcDriverLoad.h\"\n")
if(IDriver_HEADERS)
  list(REMOVE_DUPLICATES IDriver_HEADERS)
  foreach( headerString ${IDriver_HEADERS} )
    file(APPEND ${CcDriverLoad_TEMP} "#include \"Driver/${headerString}\"\n")
  endforeach()
endif()
file(APPEND ${CcDriverLoad_TEMP} "int CcDriverLoad::s_iState(-1);\n")


if(IDriver_NAMES_0)
  list(REMOVE_DUPLICATES IDriver_NAMES_0)
endif()
file(APPEND ${CcDriverLoad_TEMP} "void CcDriverLoad::load0()\n")
file(APPEND ${CcDriverLoad_TEMP} "{\n")
file(APPEND ${CcDriverLoad_TEMP} "  // Load Boot Level 0\n")
foreach( nameString ${IDriver_NAMES_0} )
  file(APPEND ${CcDriverLoad_TEMP} "  CCNEWTYPE(p${nameString}, ${nameString});\n")
  file(APPEND ${CcDriverLoad_TEMP} "  p${nameString}->entry();\n")
  file(APPEND ${CcDriverLoad_TEMP} "  m_DriverList.append(p${nameString});\n")
endforeach()
file(APPEND ${CcDriverLoad_TEMP} "}\n")


if(IDriver_NAMES_1)
  list(REMOVE_DUPLICATES IDriver_NAMES_1)
endif()
file(APPEND ${CcDriverLoad_TEMP} "void CcDriverLoad::load1()\n")
file(APPEND ${CcDriverLoad_TEMP} "{\n")
file(APPEND ${CcDriverLoad_TEMP} "  // Load Boot Level 1\n")
foreach( nameString ${IDriver_NAMES_1} )
  file(APPEND ${CcDriverLoad_TEMP} "  CCNEWTYPE(p${nameString}, ${nameString});\n")
  file(APPEND ${CcDriverLoad_TEMP} "  p${nameString}->entry();\n")
  file(APPEND ${CcDriverLoad_TEMP} "  m_DriverList.append(p${nameString});\n")
endforeach()
file(APPEND ${CcDriverLoad_TEMP} "}\n")

if(IDriver_NAMES_2)
  list(REMOVE_DUPLICATES IDriver_NAMES_2)
endif()
file(APPEND ${CcDriverLoad_TEMP} "void CcDriverLoad::load2()\n")
file(APPEND ${CcDriverLoad_TEMP} "{\n")
file(APPEND ${CcDriverLoad_TEMP} "  // Load Boot Level 2\n")
foreach( nameString ${IDriver_NAMES_2} )
  file(APPEND ${CcDriverLoad_TEMP} "  CCNEWTYPE(p${nameString}, ${nameString});\n")
  file(APPEND ${CcDriverLoad_TEMP} "  p${nameString}->entry();\n")
  file(APPEND ${CcDriverLoad_TEMP} "  m_DriverList.append(p${nameString});\n")
endforeach()
file(APPEND ${CcDriverLoad_TEMP} "}\n")

if(IDriver_NAMES_3)
  list(REMOVE_DUPLICATES IDriver_NAMES_3)
endif()
file(APPEND ${CcDriverLoad_TEMP} "void CcDriverLoad::load3()\n")
file(APPEND ${CcDriverLoad_TEMP} "{\n")
file(APPEND ${CcDriverLoad_TEMP} "  // Load Boot Level 3\n")
foreach( nameString ${IDriver_NAMES_3} )
  file(APPEND ${CcDriverLoad_TEMP} "  CCNEWTYPE(p${nameString}, ${nameString});\n")
  file(APPEND ${CcDriverLoad_TEMP} "  p${nameString}->entry();\n")
  file(APPEND ${CcDriverLoad_TEMP} "  m_DriverList.append(p${nameString});\n")
endforeach()
file(APPEND ${CcDriverLoad_TEMP} "}\n")

file(APPEND ${CcDriverLoad_TEMP} "void CcDriverLoad::load(EDeviceType eType)\n")
file(APPEND ${CcDriverLoad_TEMP} "{\n")
file(APPEND ${CcDriverLoad_TEMP} "  switch(eType)\n")
file(APPEND ${CcDriverLoad_TEMP} "  {\n")
  foreach (Device ${IDriver_ONLOAD})
    file(APPEND ${CcDriverLoad_TEMP} "    case EDeviceType::${Device}:\n")
    foreach (DeviceModule ${IDriver_ONLOAD_${Device}})
      if(LINUX)
        file(APPEND ${CcDriverLoad_TEMP} "      CcKernel::loadModule(\"lib${DeviceModule}.so\");\n")
      elseif(WINDOWS)
        file(APPEND ${CcDriverLoad_TEMP} "      CcKernel::loadModule(\"${DeviceModule}.dll\");\n")
      endif()
    endforeach()
    file(APPEND ${CcDriverLoad_TEMP} "    break;\n")
  endforeach()
  file(APPEND ${CcDriverLoad_TEMP} "    case EDeviceType::All:\n")
  file(APPEND ${CcDriverLoad_TEMP} "    default: break;\n")
file(APPEND ${CcDriverLoad_TEMP} "  }\n")
file(APPEND ${CcDriverLoad_TEMP} "}\n")

CcMoveFile(${CcDriverLoad_TEMP} ${CcDriverLoad_TARGET})

list(APPEND IDriver_SOURCES ${CcDriverLoad_TARGET} )

add_to_kernel_sources(${IDriver_SOURCES})
add_to_kernel_includes(${IDriver_INCLUDES})
add_to_kernel_definitions(${IDriver_DEFINITIONS})
add_to_kernel_dependencies(${IDriver_DEPENDENCIES})
add_to_kernel_libraries(${IDriver_LIBRARIES})

push_up_kernel_vars()
